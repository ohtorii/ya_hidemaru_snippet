/*Yet another snippet for hidemaru editor.
*/
execmacro currentmacrodirectory+"\\ya_util_get_snippet_dir.mac";
$g_snippets_abs_dir = getresultex(-1);

call GetShortPath $g_snippets_abs_dir;
$g_snippets_abs_dir = $$return;

$g_menu_text			= "";
#g_menu_text_len		= 0;

//スニペットの絶対パス
$g_abs_snippets_dir		="";
#g_abs_snippets_dir_len	= 0;


call Main;
endmacro;


Main:
	call MakeMenuText $g_snippets_abs_dir,"";
	if(! ##return){
		return false;
	}

	menuarray $g_menu_text,#g_menu_text_len;
	if(0==result){
		return false;
	}

	$$path_name = $g_abs_snippets_dir[result-1];
	execmacro currentmacrodirectory+"\\ya_set_snippet_mode.mac", $$path_name;
	return true;


MakeMenuText:
	$$root_dir 		= $$1;
	$$space 		= $$2;
	$$sub_dir		= "";
	##sub_dir_len 	= 0;

	##objFSO 		=createobject("Scripting.FileSystemObject");
	##objFolder		=member( ##objFSO, "GetFolder", $$root_dir );
	##objCollection	=getpropobj(##objFolder,"subfolders");
	while(1){
		##objItem = getcollection( ##objCollection );
		if(##objItem==0){
			break;
		}
		$$sub_dir[##sub_dir_len] = getpropstr(##objItem,"Name");
		##sub_dir_len = ##sub_dir_len + 1;
	}
	releaseobject ##objFSO;


	##index = 0;
	while (##index < ##sub_dir_len){
		$$current_sub_dir	= $$sub_dir[##index];
		$$abs_path 			= $$root_dir + "\\" + $$current_sub_dir;

		$g_menu_text[#g_menu_text_len]	= $$space + $$current_sub_dir;
		#g_menu_text_len 				= #g_menu_text_len + 1;

		$g_abs_snippets_dir[#g_abs_snippets_dir_len]= $$abs_path;
		#g_abs_snippets_dir_len 					= #g_abs_snippets_dir_len + 1;

		call MakeMenuText $$abs_path, $$space+"　　　　";
		##index = ##index + 1;
	}

	return true;


GetShortPath:
	$$path=$$1;
	##obj=createobject("Scripting.FileSystemObject");
	##f  = callmethod_returnobj( ##obj, "GetFolder", $$path);
    $$ret = getpropstr(##f,"ShortPath");
    releaseobject ##f;
    releaseobject ##obj;
    return $$ret;
