/*Yet another snippet for hidemaru editor.

スニペットのモード（text-mode/c-mode/cs-mode...）を一時的に変更する。

【主な用途】
・新規ファイル（拡張子無し）のデフォルトのスニペットを変更する。
・一時的に他のモードへ変更したいときに使用します。
*/


execmacro currentmacrodirectory+"\\ya_util_get_snippet_dir.mac";
$g_snippets_abs_dir = getresultex(-1);

call GetShortPath $g_snippets_abs_dir;
$g_snippets_abs_dir = $$return;

$g_menu_text			= "";
#g_menu_text_len		= 0;

//田楽DLLのハンドル
#g_dll_dengaku=0;

//スニペットの絶対パス
$g_abs_snippets_dir		="";
#g_abs_snippets_dir_len	= 0;


call Main;
call FreeDengaku;
endmacro;


Main:
	call LoadDengaku;
	if(! ##return){
		return false;
	}

	call MakeMenuText $g_snippets_abs_dir,"";
	if(! ##return){
		return false;
	}

	menuarray $g_menu_text,#g_menu_text_len;
	if(0==result){
		return false;
	}

	$$path_name = $g_abs_snippets_dir[result-1];
	execmacro currentmacrodirectory+"\\ya_set_snippet_mode.mac", $$path_name;
	return true;


MakeMenuText:
	$$root_dir 		= $$1;
	$$space 		= $$2;
	$$sub_dir		= "";
	##sub_dir_len 	= 0;

	call SurroundDoubleQuotation $$root_dir + "\\*.*";
	$$enume_root_dir = $$return;

	##r = dllfunc(#g_dll_dengaku,"ENUMDIR",$$enume_root_dir);
	while (1) {
	    $$dir = dllfuncstr(#g_dll_dengaku,"FINDNEXT");
	    if ($$dir == "") break;
	    else if ($$dir == "." || $$dir == "..") continue;


		$$sub_dir[##sub_dir_len] = $$dir;
		##sub_dir_len = ##sub_dir_len + 1;
	}

	/*$g_abs_snippets_dir[#g_abs_snippets_dir_len]= $$root_dir;
	#g_abs_snippets_dir_len 					= #g_abs_snippets_dir_len + 1;*/

	##index = 0;
	while (##index < ##sub_dir_len){
		$$current_sub_dir	= $$sub_dir[##index];
		$$abs_path 			= $$root_dir + "\\" + $$current_sub_dir;

		$g_menu_text[#g_menu_text_len]	= $$space + $$current_sub_dir;
		#g_menu_text_len 				= #g_menu_text_len + 1;

		$g_abs_snippets_dir[#g_abs_snippets_dir_len]= $$abs_path;
		#g_abs_snippets_dir_len 					= #g_abs_snippets_dir_len + 1;

		call MakeMenuText $$abs_path, $$space+"　　　　";
		##index = ##index + 1;
	}

	return true;

SurroundDoubleQuotation:
	return "\"" + $$1 + "\"";


LoadDengaku:
	if(#g_dll_dengaku){
		return true;
	}
	execmacro currentmacrodirectory+"\\ya_util_load_dengaku_dll.mac";
	#g_dll_dengaku=val(getresultex(-1));
	if(#g_dll_dengaku==0){
		return false;
	}
	return true;



FreeDengaku:
	if(#g_dll_dengaku!=0){
		freedll #g_dll_dengaku;
		#g_dll_dengaku=0;
	}
	return true;

GetShortPath:
	$$path=$$1;
	##obj=createobject("Scripting.FileSystemObject");
	##f  = callmethod_returnobj( ##obj, "GetFolder", $$path);
    $$ret = getpropstr(##f,"ShortPath");
    releaseobject ##f;
    releaseobject ##obj;
    return $$ret;
