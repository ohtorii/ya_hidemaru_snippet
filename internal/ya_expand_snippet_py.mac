/* Yet another snippet for hidemaru editor.
内部実装。

(.py)の展開。


＊やっていること

*/

////////////////////////////////////////////////////////////////////////////
//	Pythonの情報（後で上書きされます）
////////////////////////////////////////////////////////////////////////////
//Pythonへのパス
$g_python_exe 		= "python.exe";




////////////////////////////////////////////////////////////////////////////
//		定数
////////////////////////////////////////////////////////////////////////////
#g_args_index_num	= 14;
#g_args_index_start	= 15;


////////////////////////////////////////////////////////////////////////////
//		内部処理
////////////////////////////////////////////////////////////////////////////

//田楽DLLのハンドル
#g_dll_dengaku=0;

//スニペットファイルの絶対パス
$g_abs_snippet_filename	= "";
$g_line_string			= "";
$g_python_exe			= "";
$g_module_dir			= "";
$g_internal_dir			= "";
$g_args					= "";

call main;
endmacro str(##return);


main:
	$g_abs_snippet_filename	= getarg(0);
	$g_line_string			= getarg(2);
	$g_python_exe			= getarg(3);
	$g_module_dir			= getarg(5);
	$g_internal_dir			= getarg(6);
	
	call LoadDengaku;
	if(! ##return){
		return false;
	}

	#g_args_num = val(getarg(#g_args_index_num));
	$g_args 	= "";
	##i			= 0;
	while(##i < #g_args_num){
		$$str 	= getarg(#g_args_index_start + ##i);

		// " -> \"
		$$str 	= dllfuncstr(#g_dll_dengaku,"GSUB",$$str,"\"","\\\"",-1);
		$g_args = $g_args + "\"" + $$str + "\" ";
		##i=##i+1;
	}

	call ProcessBat;
	##ret=##return;
	call FreeDengaku;
	return ##ret;


ProcessBat:
	//
	//秀丸エディタ側の設定を一時ファイルへ保存する
	//
	execmacro currentmacrodirectory + "\\ya_util_create_temp_filename.mac";
	$$ini_filename=getresultex(-1);
	if($$ini_filename==""){
		return false;
	}
	call CreateHidemaruIniFile $$ini_filename;
	if(! ##return){
		deletefile $$ini_filename;
		return false;
	}

	//
	//引数を作る
	//
	$$cmd = "cmd.exe /c setlocal enabledelayedexpansion&&set PYTHONPATH=%PYTHONPATH%;" + 
		    $$pythonpath + "&&set YA_HIDEMARU_FILE="+$$ini_filename + "&&" + 
		    $g_python_exe + " " + $g_abs_snippet_filename + " " + $g_args;
	runex $$cmd
		, 1 			//sync	  0:async 1:sync
		, 0, "" 		//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
		, 5, "" 		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
		, 1, "" 		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
		, 1, "" 		//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
		, 2 			//show	  0:auto 1:show 2:hide
		, 1 			//nodraw  0:draw 1:no draw
		, 0 			//unicode 0:ansi 2:unicode
		;
	##ret=result;
	deletefile $$ini_filename;
	if(! ##ret){
		message("runexで失敗しました。[Python]\n"	+
				"INIファイル中の [python] セクションの記述を確認して下さい。\n"	+
				"多分、exeのパスが間違っているのかも・・・"
		);
	}
	return ##ret;


CreateHidemaruIniFile:
	/*	秀丸エディタ側の情報を記述したiniファイルを生成する
	*/
	$$out_filename=$$1;
	writeinistrw $$out_filename, "hidemaru", "line_string" , $g_line_string;
	writeinistrw $$out_filename, "hidemaru", "module_dir"  , $g_module_dir;
	writeinistrw $$out_filename, "hidemaru", "internal_dir", $g_internal_dir;
	return true;


LoadDengaku:
	if(#g_dll_dengaku!=0){
		return true;
	}
	#g_dll_dengaku=loaddll(macrodir + "\\DengakuDLL.dll");
	if (#g_dll_dengaku!=0) {
		return true;
	}
	#g_dll_dengaku=loaddll(hidemarudir + "\\DengakuDLL.dll");
	if (#g_dll_dengaku!=0) {
		return true;
	}
	message "田楽DLLのロードに失敗しました\n" + "DengakuDLL.dllが秀丸マクロディレクトリに存在するか確認してください";
	return false;

FreeDengaku:
	if(#g_dll_dengaku!=0){
		freedll #g_dll_dengaku;
		#g_dll_dengaku=0;
	}
	return true;
	