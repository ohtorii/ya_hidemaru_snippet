/* Yet another snippet for hidemaru editor.
内部実装。

(.py)の展開。


＊やっていること

*/

////////////////////////////////////////////////////////////////////////////
//	Pythonの情報（後で上書きされます）
////////////////////////////////////////////////////////////////////////////
//Pythonへのパス
$g_python_exe 		= "python.exe";




////////////////////////////////////////////////////////////////////////////
//		定数
////////////////////////////////////////////////////////////////////////////
#g_args_index_num	= 14;
#g_args_index_start	= 15;


////////////////////////////////////////////////////////////////////////////
//		内部処理
////////////////////////////////////////////////////////////////////////////

//田楽DLLのハンドル
#g_dll_hmjre=0;

//スニペットファイルの絶対パス
$g_abs_snippet_filename	= "";
$g_line_string			= "";
$g_python_exe			= "";
$g_module_dir			= "";
$g_internal_dir			= "";
$g_args					= "";

//getargのバックアップ
$g_arg_backup[0]		= "";


call main;
endmacro str(##return);


main:
	call BackupGetArg;
	call LoadDll;
	if(! ##return){
		return false;
	}

	$g_abs_snippet_filename	= $g_arg_backup[0];
	$g_line_string			= $g_arg_backup[2];
	$g_python_exe			= $g_arg_backup[3];
	$g_module_dir			= $g_arg_backup[5];
	$g_internal_dir			= $g_arg_backup[6];

	#g_args_num = val($g_arg_backup[#g_args_index_num]);
	$g_args 	= "";
	##i			= 0;
	while(##i < #g_args_num){
		$$str 	= $g_arg_backup[#g_args_index_start + ##i];
		// " -> \"
		$$str=dllfuncstr(#g_dll_hmjre,"ReplaceRegular", R"(")", $$str,0,R"(\\")",2);
		$g_args = $g_args + "\"" + $$str + "\" ";
		##i=##i+1;
	}

	call ProcessBat;
	##ret=##return;
	call FreeDll;
	return ##ret;


BackupGetArg:
	##n=argcount;
	##i=0;
	while(##i<##n){
		$g_arg_backup[##i]=getarg(##i);
		##i = ##i + 1;
	}
	return ;


ProcessBat:
	//
	//秀丸エディタ側の設定を一時ファイルへ保存する
	//

	/*Memo: execmacroを呼び出すと$g_args変数が空になる不具合を回避するパッチ*/
	$$args=$g_args;
	$g_args="";

	execmacro currentmacrodirectory + "\\ya_util_create_temp_filename.mac";
	$$ini_filename=getresultex(-1);
	if($$ini_filename==""){
		return false;
	}
	call CreateHidemaruIniFile $$ini_filename;
	if(! ##return){
		deletefile $$ini_filename;
		return false;
	}
	$$pythonpath=$g_module_dir+"\\python-util";
	//
	//引数を作る
	//
	$$cmd = "cmd.exe /c setlocal enabledelayedexpansion&&set PYTHONPATH=%PYTHONPATH%;" +
		    $$pythonpath + "&&set YA_HIDEMARU_FILE="+$$ini_filename + "&&" +
		    $g_python_exe + " " + $g_abs_snippet_filename + " " + $$args;
	runex $$cmd
		, 1 			//sync	  0:async 1:sync
		, 0, "" 		//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
		, 5, "" 		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
		, 1, "" 		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
		, 1, "" 		//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
		, 2 			//show	  0:auto 1:show 2:hide
		, 1 			//nodraw  0:draw 1:no draw
		, 0 			//unicode 0:ansi 2:unicode
		;
	##ret=result;
	deletefile $$ini_filename;
	if(! ##ret){
		message("runexで失敗しました。[Python]\n"	+
				"INIファイル中の [python] セクションの記述を確認して下さい。\n"	+
				"多分、exeのパスが間違っているのかも・・・"
		);
	}
	return ##ret;


CreateHidemaruIniFile:
	/*	秀丸エディタ側の情報を記述したiniファイルを生成する
	*/
	$$out_filename=$$1;
	writeinistrw $$out_filename, "hidemaru", "line_string" , $g_line_string;
	writeinistrw $$out_filename, "hidemaru", "module_dir"  , $g_module_dir;
	writeinistrw $$out_filename, "hidemaru", "internal_dir", $g_internal_dir;
	return true;


LoadDll:
	if(#g_dll_hmjre!=0){
		return true;
	}
	execmacro currentmacrodirectory+"\\ya_util_load_hmjre_dll.mac";
	#g_dll_hmjre=val(getresultex(-1));
	if(#g_dll_hmjre==0){
		return false;
	}
	return true;


FreeDll:
	if(#g_dll_hmjre!=0){
		freedll #g_dll_hmjre;
		#g_dll_hmjre=0;
	}
	return true;
