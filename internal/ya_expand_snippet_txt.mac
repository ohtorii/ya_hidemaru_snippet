/* Yet another snippet for hidemaru editor.
内部実装。

(.txt)の展開。
*/

////////////////////////////////////////////////////////////////////////////
//		定数
////////////////////////////////////////////////////////////////////////////
#g_args_index_num	= 14;
#g_args_index_start	= 15;





////////////////////////////////////////////////////////////////////////////
//		内部処理
////////////////////////////////////////////////////////////////////////////
//
//動作環境を固定化する。
//
setcompatiblemode
	0x00000002 |	/* 折りたたみを無視してなるべく維持 */
	0x00000008 |	/* 部分編集を無視してなるべく維持 */
	0x00000030 |	/* 同じファイルを２つ開くことを禁止する（ジャンプする） */
	0x00000200 |	/*検索での表示　→　範囲選択*/
	0x00002000 |	/*貼り付け後のカーソル位置　→　最後に移動 */
	0x00020000 ;	/*マクロ終了後に検索条件をマクロ実行前と同じに戻します。（V8.20以降） */

$g_args[0]	= "";
#g_args_num	= 0;
$g_args_line="";

call Main;
endmacro str(##return);

Main:
	$$abs_snippet_filename=getarg(0);
	call SetupArgs;
	insertfile($$abs_snippet_filename);
	call DoReplaceArg;
	call DoReplaceWild;
	call RemoveComment;
	return true;

SetupArgs:
	#g_args_num = val(getarg(#g_args_index_num));
	##i=0;
	while(##i < #g_args_num){
		$g_args[##i] = getarg(#g_args_index_start + ##i);
		##i=##i+1;
	}
	$g_args_line = getarg(2);
	return ;

//コメントの削除
RemoveComment:
	gofiletop;
	searchdown2 "^# --$",regular,nohilight;
	if(result==false){
		return;
	}
	if(selecting==0){
		return false;
	}
	beginlinesel;
	gofiletop;
	backspace;
	return;

//%0 %1 %2 ...の置換
DoReplaceArg:
	##i = 0;
	while(##i < #g_args_num){
		gofiletop;
		$$s = "%" + str(##i);
		replaceallfast $$s,$g_args[##i],nocasesense,nohilight;
		##i=##i+1;
	}
	return ;

//%* の置換
DoReplaceWild:
	gofiletop;
	replaceallfast "%*",$g_args_line,nocasesense,nohilight;
	return ;