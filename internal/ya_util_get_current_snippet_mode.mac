/*現在のスニペットモードを取得する
引数	"abs"	絶対パスで取得する (ディフォルト)
		"rel"	相対パスで取得する

返値	スニペットモード
		（絶対パスの例）	c:\hidemaru-macro\ya-hidemaru-snippet\snippets\functions\text-mode\c-mode\cpp-mode
		（相対パスの例）	text-mode\c-mode\cpp-mode
*/

$mode="";
$g_snippets_abs_dir="";
$g_ini_abs_filename="";

call main getarg(0);
endmacro $$return;

main:
	$mode=$$1;
	if($mode==""){
		$mode="abs";
	}
	call InitGlobalVariable;
	call GetExplicitSnippetMode;
	$$path =  $$return;
	if($$path!=""){
		return $$path;
	}
	call GetCurrentFileExt;
	$$ext=$$return;
	call ParseIni_Ext $$ext;
	return $$return;

InitGlobalVariable:
	execmacro currentmacrodirectory+"\\ya_util_get_snippet_dir.mac";
	$g_snippets_abs_dir = getresultex(-1);
	if($g_snippets_abs_dir == ""){
		endmacro "";
	}
	$g_snippets_abs_dir=$g_snippets_abs_dir+"\\";

	execmacro currentmacrodirectory+"\\ya_util_get_ini_filename.mac";
	$g_ini_abs_filename = getresultex(-1);
	return;

/* ya_change_mode.mac マクロでスニペットが明示されているかどうか。
*/
GetExplicitSnippetMode:
	execmacro currentmacrodirectory+"\\ya_get_snippet_mode.mac";
	$$snippet_dir_name = getresultex(-1);
	return $$snippet_dir_name;

GetCurrentFileExt:
	$$ext=filetype;
	if(0==strstr($$ext,".")){
		return $$ext;
	}
	$$ext = getinistr($g_ini_abs_filename,"new_file","ext");
	if($$ext!=""){
		return $$ext;
	}
	return "text-mode";

ParseIni_Ext:
	$$ext 		= $$1;
	$$dir_name 	= getinistr($g_ini_abs_filename, "config",$$ext);
	if($mode=="abs"){
		return $g_snippets_abs_dir + $$dir_name ;
	}
	return $$dir_name;

