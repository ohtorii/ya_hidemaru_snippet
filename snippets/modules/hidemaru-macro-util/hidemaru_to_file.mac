/*編集中の内容をファイルへ保存する

引数	保存先ファイル名
		編集中の秀丸ハンドル

返値	成功	"1"
		失敗	空文字

利用例	execmacro "hidemaru_to_file.mac", "c:\\temp\\hfoo.txt", str(##hidemaru_handle)

		// ##hidemaru_handleの内容を"c:\\temp\\hfoo.txt"へ保存する。

*/

call main, getarg(0),getarg(1);
endmacro $$return;

main:
	$$dst_filename			= $$1;
	##edit_hidemaru_handle 	= val($$2);
	##prev_hidemaru 		= hidemaruhandle(0);

	//編集中の秀丸からテキストを全て取得する。
	{
		setactivehidemaru ##edit_hidemaru_handle;
		##old_charset	= charset;
		##old_column=column;
		##old_lineno=lineno;
		selectall;
		$$text=gettext(seltopx,seltopy,selendx,selendy);
		//全選択で変化したカーソル位置を元に戻す
		movetolineno ##old_column+1, ##old_lineno;
	}

	//非表示で秀丸を開き $$text を保存する。
	{
		openfile "/h " + $$dst_filename;
		if(! result){
			message("テンポラリファイルのオープンに失敗。\n" + $$dst_filename);
			return "";
		}

		##new_hidemaru = hidemaruhandle(0);
		setencode ##old_charset,1;
		insert $$text;

		save;
	}

	//元の秀丸に戻す
	setactivehidemaru	##prev_hidemaru;
	closehidemaruforced ##new_hidemaru;
	return "1";