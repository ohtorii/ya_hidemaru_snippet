/* Yet another snippet for hidemaru editor.

（導入方法）
	キーアサインして下さい。

（メモ）
	・スニペットのコマンド数は１６個まで。($0-$15)
	  設定を変更したらExpandSnippetFile関数中のexecmacroを修正すること。
*/
disabledraw;
disableinvert;

////////////////////////////////////////////////////////////////////////////
//		内部処理
////////////////////////////////////////////////////////////////////////////
$g_dll_dengaku_abs_path = hidemarudir + "\\" + "DengakuDLL.dll";

$g_ya_hidemaru_snippet_dir	= "ya_hidemaru_snippet";
$g_snippets_dir_name 		= $g_ya_hidemaru_snippet_dir+"\\snippets";
$g_internal_module_dir		= $g_ya_hidemaru_snippet_dir+"\\internal";
$g_run_snippet_mac			= currentmacrodirectory + "\\" + $g_internal_module_dir + "\\ya_run_snippet_file.mac";


//long-file-nameのままだと、dengakuのENUMFILE関数で失敗するようなのでshort-path-nameで回避している。
call GetShortPath currentmacrodirectory + "\\" + $g_snippets_dir_name + "\\";
$g_snippets_abs_dir		= $$return + "\\";

call GetShortPath currentmacrodirectory + "\\" + $g_internal_module_dir + "\\";
$g_ini_abs_filename 	= $$return + "\\config.ini";
$g_ini_section_name		= "config";

$g_ini_section_new_file	= "new_file";
$g_ini_key_ext			= "ext";

//静的変数の名前
$g_static_variable_name	= "ya_hidemaru_snippet_path";
#g_static_variable_share_mode = 0;




$g_file_attr_arg	= ".%";
$g_file_attr_varg	= "._";


$g_args[0]	= "";
#g_args_num	= 0;
$g_args_line="";

/*スニペットファイルへの絶対パス
	(ex)
	[0]="c:/hidemaru_macro/snippets/.c.cpp/for.txt"
	[1]="c:/hidemaru_macro/snippets/.c.cpp/for.%.txt"
	[2]="c:/hidemaru_macro/snippets/.c.cpp/for.%.%.txt"
*/
$g_snippet_file[0]			= "";

/*スニペットファイルのパスと拡張子を取り除いたベース名
	(ex)
	[0]="for"
	[1]="for.%"
	[2]="for.%.%"

（備考）検索で利用。
*/
$g_snippet_file_basename[0]	= "";

/* $g_snippet_file と $g_snippet_file_basename の長さ。
*/
#g_snippet_file_num	= 0;

$_g_old_searchbuffer = searchbuffer;
#_g_searchoption 	 = searchoption;
disablehistory 0xff;
//動作環境を固定化する。
//折りたたみを維持,部分編集を維持,範囲選択,最後に移動
//マクロ終了時に元に戻る。
setcompatiblemode 0x0003|0x000c|0x0200|0x2000;
call CheckEnvironment;
if(##return){
	call LoadDengaku;
	if(##return){
		call Main;
		call ClearGlovalValue;
		setsearch $_g_old_searchbuffer, #_g_searchoption;
		freedll;
	}
}
endmacro;


Main:
	call SetGlovalValue;
	if(! ##return){
		message("setstaticvariableで失敗");
		return false;
	}

	/*if(rectselecting){
		message("矩形選択は未対応です。\n");
		return false;
	}*/
	call ParseLine;
	if(! ##return){
		return false;
	}

	call GetSnippetDir;
	$$abs_snippets_dir = $$return;

	call FindSnippetFile $$abs_snippets_dir,$g_args[0],#g_args_num;
	$$abs_snippet_filename = $$return;
	if("" == $$abs_snippet_filename){
		return false;
	}
	execmacro 	$g_run_snippet_mac,
				$$abs_snippet_filename,
				$g_args_line,
				str(#g_args_num),
				$g_args[0],
				$g_args[1],
				$g_args[2],
				$g_args[3],
				$g_args[4],
				$g_args[5],
				$g_args[6],
				$g_args[7],
				$g_args[8],
				$g_args[9],
				$g_args[10],
				$g_args[11],
				$g_args[12],
				$g_args[13],
				$g_args[14],
				$g_args[15];
	if(! ##return){
		message("execmacro に失敗しました。\nマクロ名:"+$g_run_snippet_mac);
		return false;
	}
	return val(getresultex(-1));


CheckEnvironment:
	if(! existfile($g_ini_abs_filename)){
		message("(.ini)ファイルが見つかりません。\nファイル名:"+$g_ini_abs_filename);
		return false;
	}
	return true;
	
/*１行を空白文字列(space/tab/全角空白)で分割して取り出す。
パース結果は下記グローバル変数へ格納される。
	$g_args[];
	#g_args_num;
	$g_args_line;

2011/11/05 追加：コマンドを除いた一行を$g_args_lineへ格納するようにした。
*/
ParseLine:
	##old_column=column;
	##old_lineno=lineno;
	//一行の文字列を取得
	$$linetext = gettext2(0,##old_lineno,linelen2,##old_lineno);
	$$linetext = dllfuncstr("LTRIM",$$linetext);
	$$linetext_lt = $$linetext;
	$$linetext = dllfuncstr("RTRIM",$$linetext);

	#g_args_num=0;
	$$tok = " ";
	$$tmp = dllfuncstr("GETTOKEN",$$linetext,$$tok);
	$$tmp = dllfuncstr("RTRIM",$$tmp);
	$$tmp = dllfuncstr("LTRIM",$$tmp);
	if(""!=$$tmp){
		$g_args[#g_args_num]=$$tmp;
		#g_args_num=1;
	}
	while (1) {
	    if (dllfunc("HASMORETOKENS") == 0) {
			break;
		}

	    $$tmp = dllfuncstr("GETTOKEN","",$$tok);
	    $$tmp = dllfuncstr("RTRIM",$$tmp);
		$$tmp = dllfuncstr("LTRIM",$$tmp);
		if(""!=$$tmp){
			$g_args[#g_args_num]=$$tmp;
			#g_args_num = #g_args_num+1;
		}
	}
	if(#g_args_num){
		$g_args_line = rightstr($$linetext_lt, strlen($$linetext_lt)-strlen($g_args[0]));
		$g_args_line = dllfuncstr("LTRIM",$g_args_line);
		return true;
	}
	return false;

/* ya_change_snippet_mode.mac マクロでスニペットが明示されているかどうか。
*/
GetExplicitSnippetMode:
	 $$snippet_dir_name = getstaticvariable($g_static_variable_name,#g_static_variable_share_mode);
	 call IsFolderExists $$snippet_dir_name;
	 if(##return){
		 return $$snippet_dir_name;
	 }
	 return "";

GetSnippetDir:
	call GetExplicitSnippetMode;
	$$path =  $$return;
	if(0 < strlen($$path)){
		return $$path;
	}
	call GetCurrentFileExt;
	$$ext=$$return;

	call ParseIni_Ext $$ext;
	$$abs_snippets_dir=$$return;
	return $$abs_snippets_dir;

GetCurrentFileExt:
	$$ext=filetype;
	if(0==strstr($$ext,".")){
		return $$ext;
	}
	$$ext = getinistr($g_ini_abs_filename,$g_ini_section_new_file,$g_ini_key_ext);
	if(0<strlen($$ext)){
		return $$ext;
	}
	return "text-mode";


ParseIni_Ext:
	$$ext 		= $$1;
	$$dir_name 	= getinistr($g_ini_abs_filename, $g_ini_section_name,$$ext);
	$$abs_dir 	= $g_snippets_abs_dir + $$dir_name ;
	call IsFolderExists $$abs_dir;
	if(! ##return){
		message("iniファイルに記述されているディレクトリが存在しません。\n"	+
				"iniファイルの内容を確認して下さい。\n"						+
				"\nディレクトリ="+$$dir_name);
		return "";
	}
	return $$abs_dir;

/* スニペットファイルを探す
return
	"c:/hidemaru_script/snippets/.c.cpp/for.%.%.txt"
*/
FindSnippetFile:
	$$abs_snippets_dir=$$1;
	$$cmd=$$2;
	##num=##3;

	##count=32;//無限ループ防止（念のため）
	while((0 < ##count) && (""!=$$abs_snippets_dir)){
		call FindSnippetFileInternal $$abs_snippets_dir+"\\", $$cmd, ##num;
		if("" != $$return){
			return $$return;
		}

		call GetParentFolderName $$abs_snippets_dir;
		$$abs_snippets_dir = $$return;

		call IsFolderFinish $$abs_snippets_dir;
		if(##return){
			break;
		}
		##count=##count-1;
	}
	return "";

FindSnippetFileInternal:
	$$abs_snippets_dir=$$1;
	$$cmd=$$2;
	##num=##3;

	//
	//$$cmd にマッチするファイルを列挙する。
	//
	call EnumeSnippetFileNames $$abs_snippets_dir, $$cmd;
	if(! ##return){
		return "";
	}

	//
	//完全一致を探す
	//
	call MakeSnippetBaseFileName $$cmd,##num-1;
	$$result=$$return;
	call MatchSnippetFileName $$result;
	$$result = $$return;
	if(""!=$$result){
		return $$result;
	}

	//
	//_を使ったパターンを探す。
	//(ex) 	hoge.%._
	//		hoge._
	//
	##i=##num-1;
	while(0 <= ##i){
		call MakeSnippetBaseFileName $$cmd,##i;
		$$result = $$return + $g_file_attr_varg;
		call MatchSnippetFileName $$result;
		$$result = $$return;
		if(""!=$$result){
			return $$result;
		}
		##i=##i-1;
	}
	return "";

MatchSnippetFileName:
	$$basename = $$1;
	##i=0;
	while(##i < #g_snippet_file_num){
		if($g_snippet_file_basename[##i] == $$basename){
			return $g_snippet_file[##i];
		}
		##i=##i+1;
	}
	return "";


/*ファイルのベース名を作る
(ex)
	$$cmd	= for
	##num	= 3
	return	= for.%.%.%
*/
MakeSnippetBaseFileName:
	$$cmd 	= $$1;
	##num 	= ##2;
	$$result= $$cmd;
	##i=0;
	while(##i < ##num){
		$$result=$$result+$g_file_attr_arg;
		##i=##i+1;
	}
	return $$result;

EnumeSnippetFileNames:
	$$abs_snippets_dir	= $$1;
	$$cmd 				= $$2;
	$$pattern 			= $$abs_snippets_dir + $$cmd + "*.*";

	#g_snippet_file_num=0;
	##r = dllfunc("ENUMFILE",$$pattern);
	if(! ##r){
		return false;
	}

	##count = 1000;
	while (##count) {
		$$file = dllfuncstr("FINDNEXT");
		if ($$file == "") {
			break
		}
		$g_snippet_file[#g_snippet_file_num]=$$abs_snippets_dir + $$file;
		call MakeSnippetBaseName $$file;
		$g_snippet_file_basename[#g_snippet_file_num]=$$return;
		#g_snippet_file_num=#g_snippet_file_num+1;
		##count=##count-1;
	}
	if(#g_snippet_file_num){
		return true;
	}
	return false;

/*スニペットファイルのベース名を作る
"c:/hidemaru_macro/snippets/.c.cpp/for.%.txt" → "for.%"
*/
MakeSnippetBaseName:
	$$name 	= $$1;
	##len 	= strlen($$name);

	//
	//拡張子の削除
	//
	##index=strrstr($$name,".");
	if(0 == ##index){
		//全部拡張子。とりあえず無視する。
		return "";
	}
	if(0 < ##index){
		$$name = leftstr($$name,##index);
	}
	if(0){
		//ディレクトリは無いのでコメントアウト

		//
		//ディレクトリの削除
		//
		##index_0=strrstr($$name,"/");
		##index_1=strrstr($$name,"\\");
		if(##index_0 < ##index_1){
			##index = ##index_1;
		}else{
			##index = ##index_0;
		}
		if(-1 != ##index){
			$$name=rightstr($$name,(##len-##index)+1);
		}
	}
	return $$name;

/*スニペットが置いてあるディレクトリかどうか
memo:「パス区切り(/\)・大文字小文字」の違いがあるのでファイルシステムに任せる。
*/
IsFolderFinish:
	$$dir = $$1;
	##obj 	= createobject("Scripting.FileSystemObject");
	##fld0 	= callmethod_returnobj( ##obj, "GetFolder", $g_snippets_abs_dir);
	##fld1 	= callmethod_returnobj( ##obj, "GetFolder", $$dir);
	$$path0	= getpropstr(##fld0,"Path");
	$$path1	= getpropstr(##fld1,"Path");
	releaseobject ##fld0;
	releaseobject ##fld1;
	releaseobject ##obj;
	return $$path0 == $$path1;


/*親フォルダを返す*/
GetParentFolderName:
	$$dir = $$1;
	##obj = createobject("Scripting.FileSystemObject");
	$$result = callmethod_returnstr( ##obj, "GetParentFolderName", $$dir);
	releaseobject ##obj;
	return $$result;

GetShortPath:
	$$path=$$1;
	##obj=createobject("Scripting.FileSystemObject");
	##f  = callmethod_returnobj( ##obj, "GetFolder", $$path);
    $$ret = getpropstr(##f,"ShortPath");
    releaseobject ##f;
    releaseobject ##obj;
    return $$ret;

/*フォルダの存在検査*/
IsFolderExists:
	$$dir = $$1;
	##obj = createobject("Scripting.FileSystemObject");
	##result = callmethod_returnnum( ##obj, "FolderExists", $$dir);
	releaseobject ##obj;
	return ##result;


SetGlovalValue:
	setstaticvariable "ini_abs_filename",$g_ini_abs_filename,1;
	return result;

ClearGlovalValue:
	setstaticvariable "ini_abs_filename","",1;
	return ;

LoadDengaku:
	loaddll $g_dll_dengaku_abs_path;
	if (!result) {
		message "田楽DLLのロードに失敗しました\n" + "DengakuDLL.dllが秀丸のディレクトリに存在するか確認してください";
		return false;
	}
	return true;

