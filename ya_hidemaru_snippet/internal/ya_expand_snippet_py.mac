/* Yet another snippet for hidemaru editor.
内部実装。

(.py)の展開。


＊やっていること

*/

////////////////////////////////////////////////////////////////////////////
//	Pythonの情報（後で上書きされます）
////////////////////////////////////////////////////////////////////////////
//Pythonへのパス
$g_python_exe 		= "python.exe";




////////////////////////////////////////////////////////////////////////////
//		定数
////////////////////////////////////////////////////////////////////////////
#g_args_index_num	= 14;
#g_args_index_start	= 15;


////////////////////////////////////////////////////////////////////////////
//		内部処理
////////////////////////////////////////////////////////////////////////////

//スニペットファイルの絶対パス
$g_abs_snippet_filename	= "";

call main;
endmacro str(##return);


main:
	$g_python_exe			= getarg(3);
	$g_abs_snippet_filename	= getarg(0);
	
	call LoadDengaku;
	if(! ##return){
		return false;
	}
	call Process;
	##ret=##return;

	return ##ret;



Process:
	#g_args_num = val(getarg(#g_args_index_num));
	$$args = "";
	##i=0;
	while(##i < #g_args_num){
		$$str 	= getarg(#g_args_index_start + ##i);

		// " -> \"
		$$str 	= dllfuncstr("GSUB",$$str,"\"","\\\"",-1);

		if(0 < ##i){
			$$args = $$args + " ";
		}
		$$args = $$args + "\"" + $$str + "\"";
		##i=##i+1;
	}
	$$py_filename = "\"" + $g_abs_snippet_filename + "\"";
	$$cmd = $g_python_exe + " " + $$py_filename + " " + $$args;
	runex $$cmd
		, 1 			//sync	  0:async 1:sync
		, 0, "" 		//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
		, 5, "" 		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
		, 1, "" 		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
		, 1, "" 		//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
		, 2 			//show	  0:auto 1:show 2:hide
		, 1 			//nodraw  0:draw 1:no draw
		, 0 			//unicode 0:ansi 2:unicode
		;
	if(! result){
		message("runexで失敗しました。[Python]\n"	+
				"INIファイル中の [python] セクションの記述を確認して下さい。\n"	+
				"多分、exeのパスが間違っているのかも・・・"
		);
		return false;
	}
	return true;
	

ParseIni:
	$$ini_abs_filename = $$1;
	if(! strlen($$ini_abs_filename)){
		message("内部エラー：getstaticvariable(\"ini_abs_filename\",1)が空文字を返した。\n"	+
				"作者へお問い合わせ下さい。"
		);
		return false;
	}
	if(! existfile($$ini_abs_filename)){
		message("INIファイルが見つかりません\n"	+
				"file=" + $$ini_abs_filename
		);
		return false;
	}
	
	$$section = "python";
	$$exe = getinistr($$ini_abs_filename,$$section,"exe");
	$g_python_exe = $$exe;
	return true;

LoadDengaku:
	loaddll macrodir + "\\DengakuDLL.dll";
	if (result) {
		return true;
	}
	loaddll hidemarudir + "\\DengakuDLL.dll";
	if (result) {
		return true;
	}
	message "田楽DLLのロードに失敗しました\n" + "DengakuDLL.dllが秀丸マクロディレクトリに存在するか確認してください";
	return false;
