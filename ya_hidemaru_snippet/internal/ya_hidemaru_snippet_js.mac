/* Yet another snippet for hidemaru editor.
内部実装。

(.js)の展開。
*/

////////////////////////////////////////////////////////////////////////////
//		定数
////////////////////////////////////////////////////////////////////////////
#g_args_index_num	= 14;
#g_args_index_start = 15;





////////////////////////////////////////////////////////////////////////////
//		内部処理
////////////////////////////////////////////////////////////////////////////
disabledraw;
disableinvert;
call Main;
endmacro str(##return);

Main:
	$$abs_snippet_filename=getarg(0);
	call MakeArg;
	$$arg = $$return;
	$$cmd = "cscript.exe " + $$abs_snippet_filename + " //Nologo " + $$arg;
	runex	$$cmd
			, 1 			//sync	  0:async 1:sync
			, 0, "" 		//stdin   0:none 1:auto 2:file 3:(reserve) 4:all 5:select
			, 5, "" 		//stdout  0:none 1:auto 2:file 3:add file  4:new 5:insert 6:replace
			, 1, "" 		//stderr  0:none 1:=out 2:file 3:add file  4:new 5:insert 6:replace
			, 1, "" 		//folder  0:none 1:current 2:specify 3:(reserve) 4:exe's folder
			, 2 			//show	  0:auto 1:show 2:hide
			, 1 			//nodraw  0:draw 1:no draw
			, 0 			//unicode 0:ansi 2:unicode
			;
	if(! result){
		message("runexで失敗しました。[js]");
		return false;
	}
	return true;

//return "arg1" "arg2" "arg3"
MakeArg:
	#g_args_num = val(getarg(#g_args_index_num));
	$$result	= "";
	##i 		= 0;
	while(##i < #g_args_num){
		if(##i){
			$$result=$$result+" ";
		}
		$$arg = getarg(#g_args_index_start + ##i);
		$$result = $$result  + $$arg ;
		##i=##i+1;
	}
	return $$result;
