/*
引数	mode_path,internal_dir
*/

$sep="\t";
$ret="";
call main getarg(0),getarg(1);
if(##return){
	endmacro $ret;
}
endmacro "";


main:
	$$path=$$1;
	$$internal_dir=$$2;
	if($$path==""){
		execmacro $$internal_dir+"\\ya_util_get_current_snippet_mode.mac", "rel";
		$ret=getresultex(-1);
		return true;
	}
	
	call TrimString $$path,"/\\";
	$$path=$$return;
	
	execmacro $$internal_dir+"\\ya_util_get_snippet_dir.mac";
	$$root=getresultex(-1);
	if($$root==""){
		return false;
	}else if($$path=="*"){
		call Show $$root;
	}else{
		call Show $$root+"\\"+$$path;
	}
	return ##return;


Show:
	call LoadDengaku;
	if(! ##return){
		return false;
	}
	call FindLastDirName $$1;
	$$lastdir_name=$$return;
	
	$ret = $ret + $$lastdir_name+"\n";
	call MakeText $$1,$sep;
	##ret = ##return;
	
	$ret = $ret + "%|";
	return ##ret;

MakeText:
	$$root_dir 		= $$1;
	$$space 		= $$2;
	$$sub_dir		= "";
	##sub_dir_len 	= 0;
	
	call SurroundDoubleQuotation $$root_dir + "\\*.*";
	$$enume_root_dir = $$return;

	##r = dllfunc("ENUMDIR",$$enume_root_dir);
	while (1) {
	    $$dir = dllfuncstr("FINDNEXT");
	    if ($$dir == "") {
			break;
		}else if (($$dir == ".") || ($$dir == "..")) {
			continue;
		}
		$$sub_dir[##sub_dir_len] = $$dir;
		##sub_dir_len = ##sub_dir_len + 1;
	}
	
	##index = 0;
	while (##index < ##sub_dir_len){
		$$current_sub_dir	= $$sub_dir[##index];
		$$abs_path 			= $$root_dir + "\\" + $$current_sub_dir;
	
		$ret=$ret+$$space+$$current_sub_dir+"\n";
		call MakeText $$abs_path, $$space+$sep;
		##index = ##index + 1;
	}
	return true;

SurroundDoubleQuotation:
	return "\"" + $$1 + "\"";

TrimString:
	$$space=$$2;
	if($$space==""){
		$$space="\r\n\t 　";
	}
	while(1){
	  ##word=strlen($$1);
	  //文字列の先頭に空白文字がある場合、空白文字を削除
	  if(strstr($$space,leftstr($$1,2))!=-1)$$1=rightstr($$1,##word-2);
	  else if(strstr($$space,rightstr($$1,2))!=-1)$$1=leftstr($$1,##word-2);
	  //ここまで全角文字の対処
	  else if(strstr($$space,leftstr($$1,1))!=-1)$$1=rightstr($$1,##word-1);
	  else if(strstr($$space,rightstr($$1,1))!=-1)$$1=leftstr($$1,##word-1);
	  else break;
	}
	return $$1;

LoadDengaku:
	loaddll macrodir + "\\DengakuDLL.dll";
	if (result) {
		return true;
	}
	loaddll hidemarudir + "\\DengakuDLL.dll";
	if (result) {
		return true;
	}
	//message "田楽DLLのロードに失敗しました\n" + "DengakuDLL.dllが秀丸マクロディレクトリに存在するか確認してください";
	return false;


/*パスから最後のディレクトリ名を見つける

（例）
	"c:/foo/bar"
	-> "bar"
	
	"c:/foo/bar//"
	-> "bar"
	
	"c:/foo/bar/hoge.txt"
	-> "hoge.txt"
	
	"c:"
	-> ""
*/
FindLastDirName:
	/*パス名の終端からディレクトリ区切り(\\/)を削除する
	*/
	call TrimString $$1, "\\/";
	$$dir = $$return;
	
	##index = strrstr($$dir,"\\");
	if(##index==-1){
		##index = strrstr($$dir,"/");
	}
	if(##index==-1){
		return "";
	}
	
	//ディレクトリ区切りを読み飛ばす
	##index=##index+1;
	
	return midstr($$dir,##index);
